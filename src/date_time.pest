HumanTime = {
    | DateTime
    | Date
    | ("at")? ~ Time
    | In
    | Ago
    | Now
}

DateTime = {
    | Date ~ (AtLiteral)? ~ Time
    | Time ~ (",")? ~ Date
}
IsoDate = ${ Num ~ "-" ~ Num ~ "-" ~ Num }
Date = {
    | OrdinalTimeUnitOf
    | Today
    | Tomorrow
    | Overmorrow
    | Yesterday
    | IsoDate
    | Num ~ Month_Name ~ Num
    | Num ~ Month_Name
    | RelativeSpecifier ~ Weekday
    | RelativeSpecifier ~ Week ~ Weekday
    | RelativeSpecifier ~ TimeUnit
    | ("upcoming")? ~ Weekday
}

Today = { "today" }
Tomorrow = { "tomorrow" }
Yesterday = { "yesterday" }
Overmorrow = { "overmorrow" }

Time = ${ 
    | Num ~ ( ":" ~ Num ){1,2}
}

In = { "in" ~ Duration }
Ago = { Duration ~ AgoLiteral ~ (AtLiteral? ~ HumanTime)? }
Now = { "now" }

Duration = {
    | Quantifier ~ (("," | "and")? ~ Quantifier)*
    | SingleUnit
}

SingleUnit = { Article ~ TimeUnit }

Article = _{
    | "an"
    | "a"
}


RelativeSpecifier = {
    | This
    | Next
    | Last
}

This = { "this" }
Next = { "next" }
Last = { "last" }

AgoLiteral = _{ "ago" }
AtLiteral  = _{ "at" }

Quantifier = { Num ~ TimeUnit }
Num = @{ ASCII_DIGIT+ }
TimeUnit = {
    | Year
    | Month
    | Week
    | Day
    | Hour
    | Minute
    | Second
}

Year = { "year" ~ ( "s" )? }
Month = { "month" ~ ( "s" )? }
Week = { "week" ~ ( "s" )? }
Day = { "day" ~ ( "s" )? | "d" }
Hour = { "hour" ~ ( "s" )? | "h" }
Minute = { ("minute" | "min") ~ ( "s" )? | "m" }
Second = { ("second" | "sec") ~ ( "s" )? | "s" }

Weekday = {
    | Monday
    | Tuesday
    | Wednesday
    | Thursday
    | Friday
    | Saturday
    | Sunday
}

Monday = {    
    | "monday"
    | "mon"    
}
Tuesday = {    
    | "tuesday"
    | "tue"    
}
Wednesday = {    
    | "wednesday"
    | "wed"    
}
Thursday = {    
    | "thursday"
    | "thu"    
}
Friday = {    
    | "friday"
    | "fri"    
}
Saturday = {    
    | "saturday"
    | "sat"    
}
Sunday = {    
    | "sunday"
    | "sun"    
}

Month_Name = {
    | January
    | February
    | March
    | April
    | May
    | June
    | July
    | August
    | September
    | October
    | November
    | December
}

January = {
    | "january"
    | "jan"
}    
February = {
    | "february"
    | "feb"
}    
March = {
    | "march"
    | "mar"
}    
April = {
    | "april"
    | "apr"
}    
May = {
    | "may"
    | "may"
}    
June = {
    | "june"
    | "jun"
}    
July = {
    | "july"
    | "jul"
}    
August = {
    | "august"
    | "aug"
}    
September = {
    | "september"
    | "sep"
}    
October = {
    | "october"
    | "oct"
}    
November = {
    | "november"
    | "nov"
}    
December = {
    | "december"
    | "dec"
}


OrdinalTimeUnitOf = { 
    Ordinal ~ TimeUnit ~ "of" ~ DateTimeReference
}

DateTimeReference = {
    | MonthSpec ~ YearSpec?
    | Duration ~ AgoLiteral
    | RelativeSpecifier ~ TimeUnit
    | "the" ~ TimeUnit
    | Today
    | Tomorrow  
    | Yesterday
    | Overmorrow
    | Now
}

Ordinal = { 
    | "last" 
    | NumericOrdinal 
    | WordOrdinal 
}

NumericOrdinal = @{ ASCII_DIGIT+ ~ ("st" | "nd" | "rd" | "th") }

WordOrdinal = _{
    OneToTwenty
  | TensOrdinal                    // fortieth, fiftieth, etc.
  | Tens ~ "-"? ~ UnitOrdinal      // twenty-first
  | Tens ~ "ieth"                  // twentieth
}

OneToTwenty = @{
      "first" | "second" | "third" | "fourth" | "fifth"
    | "sixth" | "seventh" | "eighth" | "ninth" | "tenth"
    | "eleventh" | "twelfth" | "thirteenth" | "fourteenth" | "fifteenth"
    | "sixteenth" | "seventeenth" | "eighteenth" | "nineteenth" | "twentieth"
}

Tens = @{ "twenty" | "thirty" | "forty" | "fifty" | "sixty" | "seventy" | "eighty" | "ninety" }
TensOrdinal = @{ "thirtieth" | "fortieth" | "fiftieth" | "sixtieth" | "seventieth" | "eightieth" | "ninetieth" }
UnitOrdinal = @{ "first" | "second" | "third" | "fourth" | "fifth" | "sixth" | "seventh" | "eighth" | "ninth" }

MonthSpec = { 
    | RelativeSpecifier ~ Month_Name 
    | RelativeSpecifier ~ "month"
    | Month_Name
    | ("the")? ~ "month"
}

YearSpec = { 
    | RelativeSpecifier ~ "year"
    | "year" ~ Num
}

WHITESPACE = _{ " " }